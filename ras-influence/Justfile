JUST := just_executable()
CORE := "2"

set quiet

[private]
default:
	{{JUST}} --list --unsorted --no-aliases

build OUTPUT_NAME="main":
	mkdir -p build/
	zig build-exe main.zig \
		-O ReleaseFast \
		-fomit-frame-pointer \
		-femit-bin=build/{{OUTPUT_NAME}}.a
	objdump -d build/{{OUTPUT_NAME}}.a > build/{{OUTPUT_NAME}}.s

bench CALL_DEPTH CALL_AMOUNT="10000" REPEAT_TIMES="10" OUTPUT_NAME="main": build
	#!/usr/bin/env bash
	GOVERNANCE=$(cat /sys/devices/system/cpu/cpu{{CORE}}/cpufreq/scaling_governor)
	THREADING=$(cat /sys/devices/system/cpu/smt/control)

	sudo cpupower -c {{CORE}} frequency-set -g performance > /dev/null
	echo off | sudo tee /sys/devices/system/cpu/smt/control > /dev/null

	sudo \
		chrt -f 99 \
		taskset -c {{CORE}} \
		build/{{OUTPUT_NAME}}.a {{CORE}} {{REPEAT_TIMES}} {{CALL_DEPTH}} {{CALL_AMOUNT}} \
		2>&1 | column -t -s ',' -o ' | '

	echo $THREADING | sudo tee /sys/devices/system/cpu/smt/control > /dev/null
	sudo cpupower -c {{CORE}} frequency-set -g $GOVERNANCE > /dev/null

clean:
	rm -rf build/

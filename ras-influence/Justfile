JUST := just_executable()
CORE := "2"
IN := "main.zig"
OUT := "build/main"

set quiet

[private]
default:
	{{JUST}} --list --unsorted --no-aliases

build:
	mkdir -p build/
	zig build-exe {{IN}} \
		-O ReleaseFast \
		-fomit-frame-pointer \
		-femit-bin={{OUT}}.a
	objdump -d {{OUT}}.a > {{OUT}}.s

bench DEPTH AMOUNT="10000" REPEAT="10": build
	#!/usr/bin/env bash
	GOVERNANCE=$(cat /sys/devices/system/cpu/cpu{{CORE}}/cpufreq/scaling_governor)
	THREADING=$(cat /sys/devices/system/cpu/smt/control)

	sudo cpupower -c {{CORE}} frequency-set -g performance > /dev/null
	echo off | sudo tee /sys/devices/system/cpu/smt/control > /dev/null

	sudo \
		chrt -f 99 \
		taskset -c {{CORE}} \
		{{OUT}}.a {{CORE}} {{REPEAT}} {{DEPTH}} {{AMOUNT}}

	echo $THREADING | sudo tee /sys/devices/system/cpu/smt/control > /dev/null
	sudo cpupower -c {{CORE}} frequency-set -g $GOVERNANCE > /dev/null

clean:
	rm -rf build/
